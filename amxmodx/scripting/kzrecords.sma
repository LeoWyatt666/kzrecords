/* Plugin generated by AMXX-Studio */

/* Thank you:
	everest,
	gladius,
	Krolik,
	PomanoB
	Garey
	kraster
*/

#include <amxmodx>
#include <curl>
#include <hamsandwich>
#include <fakemeta_util>
#include <colorchat>

#define PLUGIN "KZ Records"
#define VERSION "0.5"
#define AUTHOR "Jeronimo."

#pragma semicolon 1

/******************************************* VARS *************************************************/

new g_szMapName[32];
new bool:g_bShowRecordEnd = true;

new g_szDir[128];
new const g_szDirFile[] = "kzrecords";
new const g_szCommFile[] = "community.ini";
new const g_szCfgFile[] = "kzrecords.cfg";

new g_szCommDef[][][] =  {
	{ "xj", "Xtreme-Jumps", "https://xtreme-jumps.eu/demos.txt"},
	{ "cc", "Cosy-Climbing", "https://cosy-climbing.net/demoz.txt"}
};

enum _:COMM {
	ID,
	NAME,
	URL,
	UPDATED,
	INFO,
	DEMOS
};
new g_szComm[32][COMM][256];

enum _:CVAR {
	BOTS,
	END,
	PREFIX,
	TEAM
};
new g_cvar[CVAR];

enum _:REC {
	MAPP,
	TIME,
	JUMPER,
	COUNTRY,
	MAP,
	EXT
};

new g_iMaxPlayers;

/******************************************* BODY *************************************************/

// Init
public plugin_init() {
	register_plugin(PLUGIN, VERSION, AUTHOR);

	// Client cmd
	register_clcmd("say", "CmdSayCheck");
	register_clcmd("say_team", "CmdSayCheck");
	register_clcmd("kzr_update", "UpdateRecords", ADMIN_RCON);

	// Finish button
	RegisterHam(Ham_Use, "func_button", "hamUse");

	// Cvars
	g_cvar[PREFIX] 	= register_cvar("kzr_prefix", "[kzr]", ADMIN_RCON);
	g_cvar[END] 	= register_cvar("kzr_end", "1", ADMIN_RCON);

	g_cvar[BOTS]	= register_cvar("kzr_bots", "wr", ADMIN_RCON);
	// 0 = CS_TEAM_UNASSIGNED, 1 = CS_TEAM_T, 2 = CS_TEAM_CT, 3 = CS_TEAM_SPECTATOR
	g_cvar[TEAM] 	= register_cvar("kzr_bot_team", "1", ADMIN_RCON);

	g_iMaxPlayers = get_maxplayers();
}

public plugin_cfg() {
	new szCfgPath[96];
	get_localinfo("amxx_configsdir", szCfgPath, charsmax(szCfgPath));
	format(szCfgPath, charsmax(szCfgPath), "%s/%s", szCfgPath, g_szCfgFile);

	if(file_exists(szCfgPath)) {
		server_cmd("exec %s", szCfgPath);
		server_exec();
	}

	// Mapname
	get_mapname(g_szMapName, 31);
	strtolower(g_szMapName);

	get_localinfo("amxx_datadir", g_szDir, charsmax(g_szDir));
	format(g_szDir, charsmax(g_szDir),"%s/%s", g_szDir, g_szDirFile);

	if(!dir_exists(g_szDir))
		mkdir(g_szDir);

	// Read data comm from File;
	ReadCommunity();

	// Demos Files
	for(new i; i < sizeof(g_szComm); i++) {
		if(equal(g_szComm[i][ID],"")) continue;

		format(g_szComm[i][DEMOS], charsmax(g_szComm[][]),"%s/demos_%s.txt", g_szDir, g_szComm[i][ID]);
		format(g_szComm[i][INFO], charsmax(g_szComm[][]),"%s/info_%s.txt", g_szDir, g_szComm[i][ID]);
	}

	// BOTS
	CreateBots();
}


/********************************************* COMM ***********************************************/

// Read data comm from File;
public ReadCommunity() {
	new szFile[128];
	format(szFile, charsmax(szFile) , "%s/%s", g_szDir, g_szCommFile);

	if(!file_exists(szFile))  {
		new hFile = fopen(szFile, "wt");
		fputs(hFile, "; Format: Id (say command), Name, Demos URL^n^n");

		for( new i; i < charsmax(g_szCommDef[]); i++ ) {
			new text[128];
			format(text,127,"%s, %s, %s^n", g_szCommDef[i][0], g_szCommDef[i][1], g_szCommDef[i][2]);
			fputs(hFile, text);
		}
		fclose(hFile);
	}

	new hFile = fopen(szFile, "r");
	new szData[512];
	new num = 0;
	while(!feof(hFile))  {
		fgets(hFile, szData, charsmax(szData));
		trim(szData);

		if(!szData[0] || szData[0] == '^n' || szData[0] == ';')
			continue;

		ExplodeString(g_szComm[num], sizeof(g_szComm), charsmax(g_szComm[][]), szData, ',');
		num++;
	}
	fclose(hFile);

	return 1;
}

// Get Num By ID (say command)
stock GetCommNum(CommId[]) {
	for( new i; i < sizeof(g_szComm[]); i++ ) {
		if(equal(g_szComm[i][ID], CommId) && g_szComm[i][ID][0]) {
			return i;
		}
	}
	return -1;
}

/********************************************* SAYS ***********************************************/

// Check say cmds
public CmdSayCheck() {
	new szInMsg[64];
	read_args(szInMsg, charsmax(szInMsg));
	strtolower(szInMsg);
	remove_quotes(szInMsg);
	trim(szInMsg);

	if(szInMsg[0] != '/')
		return PLUGIN_HANDLED;

	if(szInMsg[0])
		CmdSay(0, szInMsg);

	return PLUGIN_HANDLED;
}

// function Say
public CmdSay(id, szInMsg[64]) {
	new CommId[5], WhatMap[32], CommName[256];

	replace(szInMsg, charsmax(szInMsg), "/", "");
	parse(szInMsg, CommId, charsmax(CommId), WhatMap, charsmax(WhatMap));

	if(!CommId[0])
		return PLUGIN_CONTINUE;

	if(equal(CommId, "wr")) {
		CommId = "xj";
		CommName = "World";
	}

	new num = GetCommNum(CommId);

	if(num < 0)
		return PLUGIN_CONTINUE;

	if(!equal(CommName, "World"))
		copy(CommName, charsmax(CommName), g_szComm[num][NAME]);

	if(!WhatMap[0])
		WhatMap = g_szMapName;

	new szOutMsg[192];
	new iFound = ReadDemos(num, WhatMap, szOutMsg);
	if(equal(CommId, "xj") && !iFound) {
		ReadDemos(GetCommNum("cc"), WhatMap, szOutMsg);
	}

	new prefix[16];
	get_pcvar_string(g_cvar[PREFIX], prefix, charsmax(prefix));

	new text[256];
	format(text, charsmax(text), "^x04%s ^x01%s^x03 record on ^x04%s^x03: %s", prefix, CommName, WhatMap[0], szOutMsg);
	ColorChat(id, GREY,"^x03%s", text);

	return PLUGIN_CONTINUE;
}

/********************************************* DEMOS **********************************************/

// Create message
stock ReadDemos(num, WhatMap[32], szOutMsg[192], bool:color = true, bool:one = false) {
	if(num < 0)
		return 0;

	new DataRec[6][REC][64];

	new iFound = GetRecordData(WhatMap, DataRec, g_szComm[num][DEMOS]);

	format(szOutMsg, charsmax(szOutMsg), "No record");
	for(new i; i < iFound; i++) {
		if(!DataRec[i][JUMPER][0])
			break;

		if(DataRec[i][TIME][2] != ':')
				ClimbtimeToString(str_to_float(DataRec[i][TIME]), DataRec[i][TIME], 8);

		if(DataRec[i][EXT][0] && !one) {
			new szOutAdd[32];

			if(DataRec[i][TIME][0] == '*')
				format(szOutAdd, charsmax(szOutAdd), " ^x04[%s] ^x03 No record;", DataRec[i][EXT]);
			else
				format(szOutAdd, charsmax(szOutAdd), " ^x04[%s] %s^x01 by^x04 %s %s;",
				DataRec[i][EXT], DataRec[i][TIME], DataRec[i][JUMPER], DataRec[i][COUNTRY]);

			add(szOutMsg, charsmax(szOutMsg), szOutAdd);
		}
		else {
			if(DataRec[i][TIME][0] == '*')
				format(szOutMsg, charsmax(szOutMsg), "No record");

			else
				format(szOutMsg, charsmax(szOutMsg), "^x01%s^x03 by^x04 %s %s",
				DataRec[i][TIME], DataRec[i][JUMPER], DataRec[i][COUNTRY]);
		}
	}

	if(!color) {
		replace_all(szOutMsg, charsmax(szOutMsg), "^x01", "");
		replace_all(szOutMsg, charsmax(szOutMsg), "^x03", "");
		replace_all(szOutMsg, charsmax(szOutMsg), "^x04", "");
	}

	return iFound;
}

// Get data from file
stock GetRecordData(const Map[32], DataRec[6][REC][64], RecFile[256]) {
	new i = 0, szData[64];

	new iFile = fopen(RecFile, "rt");

	if(!iFile)
		return 0;

	while(!feof(iFile)) {
		new szDataRec[REC][64];

		fgets(iFile, szData, charsmax(szData));
		trim(szData);

		if(!szData[0])
			continue;

		new szMap[2][32];

		ExplodeString(szDataRec, sizeof(szDataRec), charsmax(szDataRec[]), szData, ' ');
		ExplodeString(szMap, sizeof(szMap), charsmax(szMap[]), szDataRec[0], '[');

		if(!equal(szMap[0], Map))
			continue;

		szDataRec[MAP] = szMap[0];
		copyc(szDataRec[EXT], 8, szMap[1], ']');

		DataRec[i] = szDataRec;
		i++;
	}

	fclose(iFile);

	return i;
}

/********************************************* CURL ***********************************************/

#define CURL_BUFFER_SIZE 256

public curl_download_file(file[], link[])
{
	new data[4];

	data[0] = fopen(file, "wb");

	server_print("curl start: %s", link);

	new CURL:curl = curl_easy_init();
	curl_easy_setopt(curl, CURLOPT_BUFFERSIZE, CURL_BUFFER_SIZE);
	curl_easy_setopt(curl, CURLOPT_URL, link);
	curl_easy_setopt(curl, CURLOPT_WRITEDATA, data[0]);
	/* ATTENTION ATTENTION ATTENTION

	Without CURLOPT_NOSIGNAL on Linux Machines CURL can cause SEG FAULT even on idle state (idk why), better not remove this line

	ATTENTION ATTENTION ATTENTION*/
	curl_easy_setopt(curl, CURLOPT_NOSIGNAL, 1);
	curl_easy_setopt(curl, CURLOPT_WRITEFUNCTION, "write");
	curl_easy_perform(curl, "complite", data, sizeof(data));
}

public write(data[], size, nmemb, file)
{
	new actual_size = size * nmemb;

	fwrite_blocks(file, data, actual_size, BLOCK_CHAR);

	return actual_size;
}

public complite(CURL:curl, CURLcode:code, data[])
{
	if(code == CURLE_WRITE_ERROR)
		server_print("transfer aborted");
	else
		server_print("curl complete");

	fclose(data[0]);
	curl_easy_cleanup(curl);
}
/********************************************* UPDATE *********************************************/

public UpdateRecords() {
	new szInMsg[8];
	read_args(szInMsg, charsmax(szInMsg));
	strtolower(szInMsg);
	remove_quotes(szInMsg);
	trim(szInMsg);

	if(!szInMsg[0]) {
		for(new i; i < sizeof(g_szComm); i++) {
			if(equal(g_szComm[i][ID],"")) continue;

			curl_download_file( g_szComm[i][DEMOS], g_szComm[i][URL]);
		}
	}
	else {
		new num = GetCommNum(szInMsg);
		curl_download_file( g_szComm[num][DEMOS], g_szComm[num][URL]);
	}

	return PLUGIN_HANDLED;
}

/********************************************* FINISH *********************************************/

// Use func (bind key +use)
public hamUse(ent, id) {
	if(!get_pcvar_num(g_cvar[END]))
		return HAM_IGNORED;

	if(!(1 <= id <= get_maxplayers()))
		return HAM_IGNORED;

	new szTarget[32];
	pev(ent, pev_target, szTarget, 31);

	if((equal(szTarget, "counter_off") ||
	equal(szTarget, "clockstopbutton") ||
	equal(szTarget, "clockstop") ||
	equal(szTarget, "stop_counter") ||
	equal(szTarget, "multi_stop"))) {
		if (g_bShowRecordEnd) {
			CmdSay(id, "/wr");
			g_bShowRecordEnd = false;
			set_task(10.0, "enablrecord", 500);
		}
	}

	return HAM_IGNORED;
}

// Time task
public enablrecord() {
	g_bShowRecordEnd = true;
}

/********************************************* BOTS ***********************************************/

// BOTS
public CreateBots() {
	new cvar[32];
	get_pcvar_string(g_cvar[BOTS], cvar, charsmax(cvar));
	if(cvar[0]) {
		new CommId[8][8];
		new count = ExplodeString(CommId, 8, 8, cvar, ' ');

		for(new i; i <= count; i++) {
			new botname[64], szOutMsg[192];

			if(equal(CommId[i], "wr"))
				CommId[i] = "xj";

			new iFound = ReadDemos(GetCommNum(CommId[i]), g_szMapName, szOutMsg, false, true);
			if(equal(CommId[i], "xj") && !iFound) {
				CommId[i] = "cc";
				iFound = ReadDemos(GetCommNum(CommId[i]), g_szMapName, szOutMsg, false, true);
			}


			if(iFound) {
				format(botname, charsmax(botname), "[%s] %s", CommId[i], szOutMsg);
				CreateBot(botname);
			}
		}
	}
}

// Create FakeBot
public CreateBot(botname[]) {
	new id = engfunc(EngFunc_CreateFakeClient, botname);

	if(pev_valid(id)) {
		//Supposed to prevent crashes?
		dllfunc(MetaFunc_CallGameEntity, "player", id);
		set_pev(id, pev_flags, FL_FAKECLIENT);

		//Make Sure they have no model
		set_pev(id, pev_model, "");
		set_pev(id, pev_viewmodel2, "");
		set_pev(id, pev_modelindex, 0);

		//Make them invisible for good measure
		set_pev(id, pev_renderfx, kRenderFxNone);
		set_pev(id, pev_rendermode, kRenderTransAlpha);
		set_pev(id, pev_renderamt, 0.0);

		new team = get_pcvar_num(g_cvar[TEAM]);
		if(0 <= team <= 3) fm_cs_set_user_team(id, team);
		fm_set_user_godmode( id, 1 );
	}
}

/********************************************* FUNC ***********************************************/

// Explode
stock ExplodeString( szOutput[][], nMax, nSize, szInput[], szDelimiter ) {
	new i = 0, nLen = 0, l = strlen(szInput);
	do {
		trim(szInput[nLen]);
		nLen += (1 + copyc( szOutput[i], nSize, szInput[nLen], szDelimiter ));
	} while ((nLen < l) && (++i <= nMax));
	return i+1;
}

// Convert Time
stock ClimbtimeToString(const Float:flClimbTime, szOutPut[], const iLen) {
	if(!flClimbTime) {
		copy(szOutPut, iLen, "**:**.**");
		return;
	}

	new iMinutes = floatround(flClimbTime / 60.0, floatround_floor);
	new iSeconds = floatround(flClimbTime - iMinutes * 60, floatround_floor);
	new iMiliSeconds = floatround((flClimbTime - (iMinutes * 60 + iSeconds) ) * 100, floatround_floor);

	formatex(szOutPut, iLen, "%02i:%02i.%02i", iMinutes, iSeconds, iMiliSeconds);
}

#define PEV_PDATA_SAFE    2
#define OFFSET_TEAM            114
#define OFFSET_DEFUSE_PLANT    193
#define HAS_DEFUSE_KIT        (1<<16)
#define OFFSET_INTERNALMODEL    126

fm_cs_set_user_team(id, team)
{
    if(!(1 <= id <= g_iMaxPlayers) || pev_valid(id) != PEV_PDATA_SAFE)
    {
        return 0;
    }

    switch(team)
    {
        case 1:
        {
            new iDefuser = get_pdata_int(id, OFFSET_DEFUSE_PLANT);
            if(iDefuser & HAS_DEFUSE_KIT)
            {
                iDefuser -= HAS_DEFUSE_KIT;
                set_pdata_int(id, OFFSET_DEFUSE_PLANT, iDefuser);
            }
            set_pdata_int(id, OFFSET_TEAM, 1);
        //    set_pdata_int(id, OFFSET_INTERNALMODEL, 4);
        }
        case 2:
        {
            if(pev(id, pev_weapons) & (1<<CSW_C4))
            {
                engclient_cmd(id, "drop", "weapon_c4");
            }
            set_pdata_int(id, OFFSET_TEAM, 2);
        //    set_pdata_int(id, OFFSET_INTERNALMODEL, 6);
        }
    }

    dllfunc(DLLFunc_ClientUserInfoChanged, id, engfunc(EngFunc_GetInfoKeyBuffer, id));

    return 1;
}


/********************************************* END ************************************************/
/* AMXX-Studio Notes - DO NOT MODIFY BELOW HERE
*{\\ rtf1\\ ansi\\ deff0{\\ fonttbl{\\ f0\\ fnil Tahoma;}}\n\\ viewkind4\\ uc1\\ pard\\ lang1049\\ f0\\ fs16 \n\\ par }
*/

/* Plugin generated by AMXX-Studio */

/* Ty everest, gladius, Krolik */

#include < amxmodx >
#include < sockets >
#include < cstrike >
#include < hamsandwich >
#include < fakemeta >
#include < engine >
#include < colorchat >



#define PLUGIN "KZ Records"
#define VERSION "0.1"
#define AUTHOR "Jeronimo."

#pragma semicolon 1

/******************************************* VARS *************************************************/

new g_szMapName[32];
new Buffer[25001];
new bool:g_bShowRecordEnd = true;
new g_UpdatedNR = 1;

new g_szDir[128];
new const g_szDirFile[] = "kzrecords";
new const g_szCommFile[] = "community.ini";

new g_szCommDef[][][] =  {
	{ "wr", "World", "http://xtreme-jumps.eu/demos.txt; http://cosy-climbing.net/demos.txt"},
	{ "xj", "Xtreme-Jumps", "http://xtreme-jumps.eu/demos.txt"},
	{ "cc", "Cosy-Climbing", "http://cosy-climbing.net/demos.txt"}
};

enum _:RD {
	ID,
	NAME,
	URL,
	UPDATED,
	DEMOS
};
new g_szComm[32][RD][256];

enum _:CVAR {
	WR,
	COMM,
	END,
	PREFIX,
	TEAM
};
new g_cvar[CVAR];

/******************************************* BODY *************************************************/

// Init
public plugin_init() {
	register_plugin(PLUGIN, VERSION, AUTHOR);
	
	// Client cmd
	register_clcmd("say", "CmdSayCheck");
	register_clcmd("say_team", "CmdSayCheck");
	//register_clcmd("say /kzrecords_update", "UpdateRecords", ADMIN_RCON);
	
	// Finish button
	RegisterHam(Ham_Use, "func_button", "hamUse");
	
	// Cvars
	g_cvar[WR]	= register_cvar("kzr_bot_wr", "1", ADMIN_RCON);
	g_cvar[COMM] 	= register_cvar("kzr_bot_comm", "ru", ADMIN_RCON);
	g_cvar[END] 	= register_cvar("kzr_end", "1", ADMIN_RCON);
	g_cvar[PREFIX] 	= register_cvar("kzr_prefix", "[K-lan]", ADMIN_RCON);
	
	// 0 = CS_TEAM_UNASSIGNED, 1 = CS_TEAM_T, 2 = CS_TEAM_CT, 3 = CS_TEAM_SPECTATOR
	g_cvar[TEAM] 	= register_cvar("kzr_bot_team", "1", ADMIN_RCON);
	
	// Mapname
	get_mapname(g_szMapName, 31);
	strtolower(g_szMapName);
	
	// Read data comm from File;
	ReadCommunity();
	
	// Demos Files
	for( new i; i < sizeof(g_szComm[]); i++ ) {
		format(g_szComm[i][DEMOS], charsmax(g_szComm[][]),"%s/demos_%s.txt", g_szDir, g_szComm[i][ID]);
	}
	
	// BOTS
	new CommId[2][5];
	
	if(get_pcvar_num(g_cvar[WR]))
		CommId[0] = "wr";
	
	new cvar[5];
	get_pcvar_string(g_cvar[COMM], cvar, charsmax(cvar));
	if(cvar[0])
		CommId[1] = cvar;
	
	for(new i; i < sizeof(CommId); i++) {
		if(CommId[i][0]) {
			new botname[128], szOutMsg[192];
			ReadDemos(GetCommNum(CommId[i]), g_szMapName, szOutMsg, false, true);
			format(botname, charsmax(botname), "[%s] %s", CommId[i], szOutMsg);
			CreateBot(botname);
		}
	}
	
	// Update
	/*	
	format(LastUpdate, 127, "%s/demos_last_update.ini", g_szDir);
	
	if( !filexists( LastUpdate ) ) {
		
		UpdateRecords( );
		
		return;
	}
	
	new iYear, iMonth, iDay, szDate[ 11 ];
	date( iYear, iMonth, iDay );
	
	new iFile = fopen( LastUpdate, "rt" );
	fgets( iFile, szDate, 10 );
	fclose( iFile );
	
	if( iYear > str_to_num( szDate[ 0 ] ) || iMonth > str_to_num( szDate[ 5 ] ) || iDay > str_to_num( szDate[ 8 ] ) )
	UpdateRecords();
	*/
		
}

// Read data comm from File;
public ReadCommunity() {
	get_localinfo("amxx_datadir", g_szDir, charsmax(g_szDir));
	format(g_szDir, charsmax(g_szDir),"%s/%s", g_szDir, g_szDirFile);
	
	if(!dir_exists(g_szDir))
		mkdir(g_szDir);
		
	new szFile[128];
	format(szFile, charsmax(szFile) , "%s/%s", g_szDir, g_szCommFile);
	
	if(!file_exists(szFile))  {
		new hFile = fopen(szFile, "wt");
		fputs(hFile, "; Format: ^"Id (say command)^" ^"Name^" ^"Demos URL; and more...^"^n^n");
		
		for( new i; i < charsmax(g_szCommDef[]); i++ ) {
			new text[128];
			format(text,127,"^"%s^" ^"%s^" ^"%s^"^n", g_szCommDef[i][0], g_szCommDef[i][1], g_szCommDef[i][2]);
			fputs(hFile, text);
		}
		fclose(hFile);	
	}
	
	new hFile = fopen(szFile, "r");
	new szData[512];
	new i;
	while(!feof(hFile))  {
		fgets(hFile, szData, charsmax(szData));
		trim(szData);
				
		if(!szData[0] || szData[0] == '^n' || szData[0] == ';')  {
			continue;
		}
		
		parse(szData, g_szComm[i][ID], 5,  g_szComm[i][NAME], 32, g_szComm[i][URL], 256);
		i++;
	}
	fclose(hFile);
}

// Check say cmds
public CmdSayCheck() {
	new szInMsg[64];
	read_args(szInMsg, charsmax(szInMsg));
	strtolower(szInMsg);
	remove_quotes(szInMsg);
	trim(szInMsg);
	
	if(szInMsg[0] != '/')
		return PLUGIN_HANDLED;
		
	if(szInMsg[0])
		CmdSay(0, szInMsg);
		
	return PLUGIN_HANDLED;
}

// function Say
public CmdSay(id, szInMsg[64]) {
	new CommId[5], WhatMap[32]; 
	if(szInMsg[0] == '/') {	
		replace(szInMsg, charsmax(szInMsg), "/", "");
		parse(szInMsg, CommId, charsmax(CommId), WhatMap, charsmax(WhatMap));
	}
	
	new num = GetCommNum(CommId);
	if(num >= 0) {
		if(!WhatMap[0])
			WhatMap = g_szMapName;
		
		new szOutMsg[192];
		ReadDemos(num, WhatMap, szOutMsg);
		
		new prefix[16];
		get_pcvar_string(g_cvar[PREFIX], prefix, charsmax(prefix));
		
		new text[256];
		format(text, charsmax(text), "^x04%s^x03 %s record on ^x04%s^x03: %s", prefix, g_szComm[num][NAME], WhatMap[0], szOutMsg);
		ColorChat(id, GREY,"^x03%s", text);
	}
	
	return PLUGIN_CONTINUE;
}

// Create message
stock ReadDemos(num, WhatMap[32], szOutMsg[192], bool:color = true, bool:one = false) {
	if(num<0)
		return;
		
	new Author[6][32], Time[6][9], Extension[6][8];
	
	new iFound = GetRecordData(WhatMap, Author, Time, Extension, g_szComm[num][DEMOS]);
	
	if(iFound > 0) {
		for(new i; i < iFound; i++) {
			if( !Author[i][0] )
				break;
			
			if(Extension[i][0] && !one) {
				new szOutAdd[32];
				
				if(Time[i][0] == '*')
					format(szOutAdd, charsmax(szOutAdd), " ^x04[%s] ^x03 No record;", Extension[i]);
				else
					format(szOutAdd, charsmax(szOutAdd), " ^x04[%s] %s^x03 by^x04 %s;", Extension[i], Time[i], Author[i]);
					
				add(szOutMsg, charsmax(szOutMsg), szOutAdd);
			}
			else {
				if(Time[i][0] == '*')
					format(szOutMsg, charsmax(szOutMsg), "No record");
					
				else
					format(szOutMsg, charsmax(szOutMsg), "^x04%s^x03 by^x04 %s", Time[i], Author[i]);
			}	
		}
	}
	else
		format(szOutMsg, charsmax(szOutMsg), "No record");
	
	if(!color) {
		replace_all(szOutMsg, charsmax(szOutMsg), "^x04", "");
		replace_all(szOutMsg, charsmax(szOutMsg), "^x03", "");
	}
	
	return;
}

// Get Num By ID (say command)
stock GetCommNum(CommId[]) {	
	for( new i; i < sizeof(g_szComm[]); i++ ) {
		if(equal(g_szComm[i][ID], CommId) && g_szComm[i][ID][0]) {
			return i;
		}
	}
	return -1;
}

// Get data from file
stock GetRecordData(const Map[32], Jumper[6][32], Time[6][9], Extension[6][8], RecFile[256]) {
	new szData[64], szMap[32], szTime[9], iFounds, iLen, iMapLen = strlen(Map);
	
	new iFile = fopen(RecFile, "rt");
	
	if(!iFile)
		return 0;
	
	while(!feof(iFile)) {
		fgets(iFile, szData, 63);
		trim(szData);
		
		if(!szData[0] || !equali(szData, Map, iMapLen))
			continue;
		
		iLen = 1 + copyc(szMap, 31, szData, ' ');
		
		if( szMap[iMapLen] != '[' && iMapLen != strlen(szMap) )
			continue;
		
		iLen += 1 + copyc(szTime, 8, szData[iLen], ' ');
		copy( Jumper[iFounds], 32, szData[iLen]);
		
		if(szTime[2] != ':')
			ClimbtimeToString(str_to_float(szTime), szTime, 8);
		
		copy(Time[iFounds], 8, szTime);
		
		//Time[iFounds] = str_to_num(szTime);
		
		if(szMap[iMapLen] == '[')
			copyc(Extension[iFounds], 8, szMap[iMapLen+1], ']');
		
		iFounds++;
	}
	
	fclose(iFile);
	
	return iFounds;
}

// Convert Time
stock ClimbtimeToString(const Float:flClimbTime, szOutPut[], const iLen) {
	if(!flClimbTime) {
		copy(szOutPut, iLen, "**:**.**");
		return;
	}
	
	new iMinutes = floatround(flClimbTime / 60.0, floatround_floor);
	new iSeconds = floatround(flClimbTime - iMinutes * 60, floatround_floor);
	new iMiliSeconds = floatround((flClimbTime - (iMinutes * 60 + iSeconds) ) * 100, floatround_floor);
	
	formatex(szOutPut, iLen, "%02i:%02i.%02i", iMinutes, iSeconds, iMiliSeconds);
}


/********************************************* UPDATE *********************************************/

// Read from site
public ReadWeb(const iSocket) {
	new RecFile[256];
	for( new i; i < charsmax(g_szComm[]); i++ ) {
		if ( g_UpdatedNR == i )
			RecFile = g_szComm[i][DEMOS];
	}

	g_UpdatedNR++;
	
	while (socket_recv( iSocket, Buffer, 25000 )) {
		if( Buffer[ 0 ] ) {
			if( Buffer[ 0 ] == 'H' && Buffer[ 1 ] == 'T' ) { // Header
				new iPos;
				iPos  = contain( Buffer, "^r^n^r^n" )   + 4;
				iPos += contain( Buffer[ iPos ], "^n" ) + 1;
				
				formatex( Buffer, charsmax( Buffer ), Buffer[ iPos ] );
			}
			
			new iFile = fopen( RecFile, "at" );
			fputs( iFile, Buffer );
			fclose( iFile );		
		}
	}
	
	Buffer[ 0 ] = 0; // Clean the memory.
	socket_close(iSocket);
}

// Update function
public UpdateRecords( ) {
	for( new i; i < charsmax(g_szComm[]); i++ ) {
		if (file_exists(g_szComm[i][DEMOS]))
			delete_file(g_szComm[i][DEMOS]);
	}
	
	new iYear, iMonth, iDay, szTemp[ 11 ];
	date( iYear, iMonth, iDay );
	
	new LastUpdate[64];
	new iFile = fopen( LastUpdate, "wt" );
	formatex( szTemp, 10, "%04ix%02ix%02i", iYear, iMonth, iDay );
	fputs( iFile, szTemp );
	fclose( iFile );
	
	new Host[ 96 ], Url[ 96 ], Socket[ 256 ], iPos, iSocket;
	
	for( new i; i < charsmax(g_szComm[]); i++ ) {
		copy( Host, 95,  g_szComm[i][URL] );
		iPos = contain( Host, "/" );
		
		if( iPos != -1 ) {
			copy( Url, 95, Host[ iPos + 1 ] );
			Host[ iPos ] = 0;
		}
		
		iSocket = socket_open( Host, 80, SOCKET_TCP, iPos );
		
		if( iPos > 0 )
			continue;
		
		formatex( Socket, 255, "GET /%s HTTP/1.1^nHost: %s^r^n^r^n", Url, Host );
		
		socket_send( iSocket, Socket, 255 );
		
		set_task( 0.25, "ReadWeb", iSocket );
	}	
}

/********************************************* FINISH *********************************************/

// Use func
public hamUse(ent, id) {
	if(!get_pcvar_num(g_cvar[END]))
		return HAM_IGNORED;
	
	if(!(1 <= id <= get_maxplayers()))
		return HAM_IGNORED;
	
	new szTarget[32];
	pev(ent, pev_target, szTarget, 31);
	
	if((equal(szTarget, "counter_off") || 
	equal(szTarget, "clockstopbutton") || 
	equal(szTarget, "clockstop") || 	
	equal(szTarget, "stop_counter") || 
	equal(szTarget, "multi_stop"))) {
		if (g_bShowRecordEnd) {
			CmdSay(id, "/wr");
			g_bShowRecordEnd = false;
			set_task(10.0, "enablrecord", 500);
		}
	}
	
	return HAM_IGNORED;
}

// Time task
public enablrecord() {
	g_bShowRecordEnd = true;
}

/********************************************* BOTS ***********************************************/

// Create FakeBot
public CreateBot(botname[]) {
	new id = engfunc(EngFunc_CreateFakeClient, botname);
	
	if(pev_valid(id)) {	
		//Supposed to prevent crashes?
		dllfunc(MetaFunc_CallGameEntity, "player", id);
		set_pev(id, pev_flags, FL_FAKECLIENT);

		//Make Sure they have no model
		set_pev(id, pev_model, "");
		set_pev(id, pev_viewmodel2, "");
		set_pev(id, pev_modelindex, 0);

		//Make them invisible for good measure
		set_pev(id, pev_renderfx, kRenderFxNone);
		set_pev(id, pev_rendermode, kRenderTransAlpha);
		set_pev(id, pev_renderamt, 0.0);
		
		new team = get_pcvar_num(g_cvar[TEAM]);
		console_print(0, "---------- %d ---", team);
		if(0 <= team <= 3) cs_set_user_team(id, team);
	}
}

/********************************************* END ************************************************/
